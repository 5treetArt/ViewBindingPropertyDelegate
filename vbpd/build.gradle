plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

ext {
    groupId = 'com.kirich1409.viewbindingpropertydelegate'
    artifactId = "viewbindingpropertydelegate"
    bintrayPackageName = "viewbindingpropertydelegate"
}

apply from: rootProject.file('publishing.gradle')

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs +=
                ['-module-name', "com.github.kirich1409.ViewBindingPropertyDelegate"]
    }

    buildFeatures {
        buildConfig = false
        viewBinding = true
        dataBinding = false
        aidl = false
        renderScript = false
        resValues = false
        shaders = false
    }

    sourceSets {
        test {
            java.srcDir 'src/sharedTest/java'
        }
        androidTest {
            java.srcDir 'src/sharedTest/java'
        }
    }

    testOptions {
        // include the android resources in local tests. See http://robolectric.org/migrating/#migrating-to-40
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation rootProject.ext.dependencies.coreKtx
    implementation rootProject.ext.dependencies.fragmentKtx
    implementation rootProject.ext.dependencies.kotlinStdlib
    implementation rootProject.ext.dependencies.viewBinding
    api project(":vbpd-noreflection")

    testImplementation 'org.robolectric:robolectric:4.3'

    testImplementation rootProject.ext.dependencies.fragmentTesting

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13'

    // Core library
    testImplementation 'androidx.test:core:1.3.0'

    // AndroidJUnitRunner and JUnit Rules
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation 'androidx.test:rules:1.3.0'

    // Assertions
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test.ext:truth:1.3.0'
    testImplementation 'com.google.truth:truth:1.0.1'

    // Required -- JUnit 4 framework
    androidTestImplementation 'junit:junit:4.13'

    // Core library
    androidTestImplementation 'androidx.test:core:1.3.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
}
